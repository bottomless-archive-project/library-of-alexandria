import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        set('artemis.version', '2.14.0')
        set('elasticsearch.version', '7.9.2')
        set('mongodb.version', '4.1.0')
        set('mongo-driver-reactivestreams.version', '4.1.1')
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:2.3.4.RELEASE')
        classpath('com.adarshr:gradle-test-logger-plugin:2.0.0')
        classpath('com.github.ben-manes:gradle-versions-plugin:0.29.0')
        classpath('com.palantir.gradle.docker:gradle-docker:0.25.0')
    }
}

subprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(15)
        }
    }

    compileJava.dependsOn(processResources)
    compileJava {
        options.encoding = "UTF-8"
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'pmd'

    pmd {
        ruleSets = []
        ruleSetFiles = files("${project.rootProject.projectDir}/analysis/pmd/ruleset.xml")
    }

    apply plugin: 'checkstyle'
    checkstyle {
        configFile = file("${project.rootProject.projectDir}/analysis/checkstyle/checkstyle.xml")
    }

    dependencies {
        // Spring starters
        compile('org.springframework.boot:spring-boot-starter')

        // Configuration
        compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
        annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

        // Lombok
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'

        // Testing
        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
        testCompile group: 'io.projectreactor', name: 'reactor-test'
    }

    apply plugin: "com.github.ben-manes.versions"
}
