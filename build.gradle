import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        set('artemis.version', '2.31.0')
        set('mongodb.version', '4.10.2')
        set('okhttp.version', '4.10.0')
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:3.1.4')
        classpath('com.adarshr:gradle-test-logger-plugin:2.0.0')
        classpath('com.github.ben-manes:gradle-versions-plugin:0.46.0')
        classpath('com.palantir.gradle.docker:gradle-docker:0.25.0')
    }
}

configure(subprojects.findAll { it.name != 'loa-frontend' }) {
    apply plugin: 'java'

    java {
        modularity.inferModulePath = true
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        flatDir {
            dirs "$rootProject.projectDir/libs"
        }
    }

    compileJava.dependsOn(processResources)
    compileJava {
        options.encoding = "UTF-8"
        doFirst {
            options.compilerArgs += [
                    '--enable-preview', '-parameters'
            ]
        }
    }
    compileTestJava {
        options.encoding = "UTF-8"
        doFirst {
            options.compilerArgs += [
                    '--enable-preview'
            ]
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform()
    }

    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'pmd'

    pmd {
        ruleSets = []
        ruleSetFiles = files("${project.rootProject.projectDir}/analysis/pmd/ruleset.xml")
    }

    apply plugin: 'checkstyle'
    checkstyle {
        configFile = file("${project.rootProject.projectDir}/analysis/checkstyle/checkstyle.xml")
    }

    dependencies {
        // Spring starters
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter'

        // Configuration
        compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
        annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

        // Lombok
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'

        // Testing
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    }

    apply plugin: "com.github.ben-manes.versions"
}
