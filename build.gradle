import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        set('artemis.version', '2.12.0')
        set('elasticsearch.version', '7.6.0')
        set('mongodb.version', '4.0.3')
        set('mongo-driver-reactivestreams.version', '4.0.3')
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:2.3.0.RELEASE')
        classpath('com.adarshr:gradle-test-logger-plugin:2.0.0')
        classpath('com.github.ben-manes:gradle-versions-plugin:0.28.0')
        classpath('com.palantir.gradle.docker:gradle-docker:0.25.0')
    }
}

subprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_13
        targetCompatibility = JavaVersion.VERSION_13
    }

    compileJava.dependsOn(processResources)
    compileJava {
        options.encoding = "UTF-8"
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'pmd'

    pmd {
        ruleSets = []
        ruleSetFiles = files("${project.rootProject.projectDir}/analysis/pmd/ruleset.xml")
    }

    dependencies {
        // Spring starters
        compile('org.springframework.boot:spring-boot-starter')

        // Configuration
        compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
        annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

        // Lombok
        compileOnly group: 'org.projectlombok', name: 'lombok'
        annotationProcessor group: 'org.projectlombok', name: 'lombok'
        testCompileOnly group: 'org.projectlombok', name: 'lombok'
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

        // Testing
        testCompile group: 'org.mockito', name: 'mockito-core'
        testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

        testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
        testCompile group: 'io.projectreactor', name: 'reactor-test'
    }

    apply plugin: "com.github.ben-manes.versions"
}
