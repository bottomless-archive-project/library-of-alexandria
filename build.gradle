import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.1.5.RELEASE'

        set('elasticsearch.version', '7.0.1')
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.adarshr:gradle-test-logger-plugin:1.6.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
    }
}

subprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    apply plugin: 'java'

    compileJava.dependsOn(processResources)
    compileJava {
        options.encoding = "UTF-8"
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    apply plugin: "com.adarshr.test-logger"

    dependencies {
        // Spring starters
        compile('org.springframework.boot:spring-boot-starter')

        // Statistics
        compile group: 'io.micrometer', name: 'micrometer-core'

        // Configuration
        annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')

        // Lombok
        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')

        // Testing
        testCompile group: 'org.mockito', name: 'mockito-core'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    apply plugin: "com.github.ben-manes.versions"
}
