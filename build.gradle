import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    ext {
        springBootVersion = '2.2.4.RELEASE'

        set('artemis.version', '2.11.0')
        set('elasticsearch.version', '7.5.0')
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.adarshr:gradle-test-logger-plugin:1.6.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
    }
}

subprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }

    apply plugin: 'java'

    java {
        sourceCompatibility = JavaVersion.VERSION_13
        targetCompatibility = JavaVersion.VERSION_13
    }

    compileJava.dependsOn(processResources)
    compileJava {
        options.encoding = "UTF-8"
    }
    compileTestJava {
        options.encoding = "UTF-8"
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    apply plugin: "com.adarshr.test-logger"

    dependencies {
        // Spring starters
        compile('org.springframework.boot:spring-boot-starter')

        // Configuration
        compileOnly group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
        annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

        // Lombok
        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')

        // Testing
        testCompile group: 'org.mockito', name: 'mockito-core'
        testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.1'

        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
        testCompile group: 'io.projectreactor', name: 'reactor-test'
    }

    apply plugin: "com.github.ben-manes.versions"
}
